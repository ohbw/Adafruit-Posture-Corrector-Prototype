#include <Adafruit_CircuitPlayground.h>

#define VIBRATION_MOTOR_PIN 10    // Pin for the vibration motor
#define SITTING_THRESHOLD 9.6    // Acceleration threshold for detecting sitting (m/s²)
#define MOVEMENT_THRESHOLD 10.0    // Acceleration threshold for detecting movement (m/s²)
#define SITTING_TIME_LIMIT 900000 // 15 minutes in milliseconds
#define BAD_POSTURE_TIME_LIMIT 10000 // 10 seconds in milliseconds
#define SITTING_DETECTION_TIME 5000 // 5 seconds in milliseconds

unsigned long currentMillis = 0; // Variable to store the current time
unsigned long sittingStillStartTime = 0;   // Track when the user stopped moving
unsigned long sittingStartTime = 0;        // Track when the user started sitting
unsigned long badPostureStartTime = 0;     // Track when bad posture starts

// Flags
bool isSitting = false;
bool inBadPosture = false;
bool isStill = false;
bool hasPlayedSound = false;
bool sliderOn = false;

int melodyOption = 0;

float referenceTiltAngle = 90.0; // Default "good" posture angle before calibration

float tilt;
float X, Y, Z, totalAccel;

unsigned long previousMillis = 0;
const long oneSecondInterval = 1000;
unsigned long sliderMillis = 0;

unsigned long buttonPressMillis = 0;
unsigned long button2PressMillis = 0;

// Function to activate the vibration motor
void activateVibrationMotor() {
  digitalWrite(VIBRATION_MOTOR_PIN, HIGH);  // Turn on the motor
}

// Function to deactivate the vibration motor
void deactivateVibrationMotor() {
  digitalWrite(VIBRATION_MOTOR_PIN, LOW);   // Turn off the motor
}

// Function to set all LEDs to red for bad posture
void setLEDsBadPosture() {
  for (int i = 0; i < 10; i++) {
    CircuitPlayground.setPixelColor(i, 255, 0, 0);  // Red for bad posture
  }
}

// Function to set all LEDs to green for good posture
void setLEDsGoodPosture() {
  for (int i = 0; i < 10; i++) {
    CircuitPlayground.setPixelColor(i, 0, 255, 0);  // Green for good posture
  }
}

// Function to play melody 1
void playMelody1() {
  int melody1[] = {262, 294, 330,};  // C4, D4, E4
  int noteDurations1[] = {500, 500, 500};  // Note durations in ms
  playMelody(melody1, noteDurations1);
}

// Function to play melody 2
void playMelody2() {
  int melody2[] = {392, 440, 494};  // G4, A4, B4
  int noteDurations2[] = {400, 400, 400};  // Note durations in ms
  playMelody(melody2, noteDurations2);
}

// Function to play melody 3
void playMelody3() {
  int melody3[] = {523, 587, 659};  // C5, D5, E5
  int noteDurations3[] = {600, 600, 600};  // Note durations in ms
  playMelody(melody3, noteDurations3);
}

// General function to play melodies
void playMelody(int melody[], int noteDurations[]) {
  for (int i = 0; i < 3; i++) {
    CircuitPlayground.playTone(melody[i], noteDurations[i]);
  }
}

void setup() {
  CircuitPlayground.begin();             // Initialize Circuit Playground
  Serial.begin(9600);                    // Start Serial communication
  pinMode(VIBRATION_MOTOR_PIN, OUTPUT);  // Set vibration motor pin as output
  digitalWrite(VIBRATION_MOTOR_PIN, LOW); // Turn off vibration motor initially
  CircuitPlayground.clearPixels();       // Clear all LEDs initially
}

void loop() {
  currentMillis = millis();  // Get the current time

  // Check slider value every 1 second
  if (currentMillis - sliderMillis >= oneSecondInterval) {
    sliderMillis = currentMillis;  // Update the last slider check time
    sliderOn = CircuitPlayground.slideSwitch();  // Read the slider state
  }

  if (sliderOn) {
    currentMillis = millis();  // Get the current time
    if ((currentMillis - previousMillis >= oneSecondInterval)) {
      previousMillis = currentMillis;  // Update the last action time

      // Get accelerometer data for X, Y, Z axes
      X = CircuitPlayground.motionX();
      Y = CircuitPlayground.motionY();
      Z = CircuitPlayground.motionZ();

      // Calculate total acceleration (net acceleration) magnitude
      totalAccel = sqrt(X * X + Y * Y + Z * Z);

      // Calculate tilt angle in degrees
      tilt = acos(Z / 9.8) * (180.0 / PI);

      // Compare tilt with reference angle to determine posture
      if (abs(tilt - referenceTiltAngle) > 18.3) {  // 18.3 degrees threshold for bad posture
        if (!inBadPosture) {
          // User just entered bad posture
          inBadPosture = true;
          badPostureStartTime = millis();  // Start bad posture timer
        } else {
          // Check if bad posture has persisted for 10 seconds
          if (millis() - badPostureStartTime >= BAD_POSTURE_TIME_LIMIT) {
            activateVibrationMotor();  // Trigger vibration motor after 10 seconds
            Serial.println("Bad posture detected for more than 10 seconds.");
          }
        }
        setLEDsBadPosture();  // Turn all LEDs red for bad posture
      } else { // Tilt in good posture
        if (inBadPosture) {
          // Reset bad posture state
          inBadPosture = false;
          deactivateVibrationMotor();  // Stop vibration motor
        }
        setLEDsGoodPosture();  // Turn all LEDs green for good posture
      }

      // Check if the user is sitting or moving
      if (totalAccel > MOVEMENT_THRESHOLD) {
        // User is moving, reset still timer
        isStill = false;
        isSitting = false;
      } else {
        // User is still, start or continue the stillness timer
        if (!isStill) {
          isStill = true;
          sittingStillStartTime = millis();  // Start tracking stillness
        }

        // Check if the user has been still for 5 seconds
        if (millis() - sittingStillStartTime >= SITTING_DETECTION_TIME) {
          if (!isSitting) {
            // User just started sitting after being still for 5 seconds
            isSitting = true;
            sittingStartTime = millis();  // Start sitting timer
          }
        }
      }

      // Feedback if sitting too long
      if (isSitting) {
        // Check if the user has been sitting for 15 minutes
        if (millis() - sittingStartTime >= SITTING_TIME_LIMIT) {
          // Play the selected melody
          if (melodyOption == 0) {
            playMelody1();
          } else if (melodyOption == 1) {
            playMelody2();
          } else if (melodyOption == 2) {
            playMelody3();
          }
      }
    }
  } else {
    CircuitPlayground.clearPixels();  // Turn off all LEDs when slider is off
  }

  // Melody button press detection
  if (CircuitPlayground.leftButton() && (currentMillis - buttonPressMillis > oneSecondInterval)) {
    melodyOption = (melodyOption + 1) % 3;  // Cycle between 0, 1, and 2 for melody selection
    buttonPressMillis = currentMillis;
    Serial.print("Melody option changed to: ");
    Serial.println(melodyOption);
  }

  if (CircuitPlayground.rightButton() && (currentMillis - button2PressMillis > oneSecondInterval)) {
    referenceTiltAngle = tilt;
    Serial.print("Tilt reference angle updated: ");
    Serial.println(referenceTiltAngle);
    button2PressMillis = currentMillis;
  }
}
